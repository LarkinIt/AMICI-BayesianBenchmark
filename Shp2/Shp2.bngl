# Base model of Shp2 regulation from Barua, Faeder, and Haugh (2006).
# Copyright 2006, North Carolina State University and Los Alamos National
# Laboratory

# Concentration units are in micromolar; time units are in seconds.
begin model
begin parameters
    kdim         3

    kopen        1
    kclose       2.69

    kon_CSH2     0       
    koff_CSH2    0        

    kon_NSH2     0       
    koff_NSH2    0

    kkin_Y1      -1

    kon_PTP      0        
    koff_PTP     1       
    kcat_PTP     0        

    chi_r1       3
    chi_r2       2
    chi_r3       3
    chi_r4       2
    chi_r5       2
    chi_r6       2
    chi_r7       2
    chi_r8       3  # Equals chi_r1*chi_r6/chi_r2
    chi_r9       2   # Equals chi_r1*chi_r7/chi_r3
    chi_r10      2   # Equals chi_r1*chi_r6/chi_r4
    chi_r11      3  # Equals chi_r1*chi_r7/chi_r5

    R_dim        -1.6021 # R_tot= 2*R_dim
    S_tot        -1.3010
end parameters

begin molecule types
    R(DD,Y1~U~P,Y2~P)
    S(NSH2~C~O,CSH2,PTP~C~O)
end molecule types

begin seed species
    S(NSH2~C,CSH2,PTP~C)                 10^S_tot
    # Pre-dimerized receptors
    R(DD!1,Y1~U,Y2~P).R(DD!1,Y1~U,Y2~P)  10^R_dim
end seed species

begin reaction rules
    # Intra-complex phosphorylation 
    R1: R(DD!+,Y1~U) -> R(DD!+,Y1~P)  10^kkin_Y1

    # Equilibrium between the closed form and open form of S
    R2: S(NSH2~C,PTP~C) <-> S(NSH2~O,PTP~O)  10^kopen, 10^kclose

    # Binding of S(CSH2) from cytosol
    R3: R(Y2~P) + S(CSH2) <-> R(Y2~P!1).S(CSH2!1)  10^kon_CSH2,10^koff_CSH2 \
    exclude_reactants(2,R)

    # Binding of S(NSH2~O) from cytosol
    R4: R(Y2~P) + S(NSH2~O) <-> R(Y2~P!1).S(NSH2~O!1)  10^kon_NSH2,10^koff_NSH2 \
    exclude_reactants(2,R)

    # Binding of S(PTP~O) from cytosol
    R5: R(Y1~P) + S(PTP~O) <-> R(Y1~P!1).S(PTP~O!1)  10^kon_PTP,10^koff_PTP \
    exclude_reactants(2,R)

    # Dephosphorylation of R(Y1~P)
    R6A: R(Y1~P!1).S(PTP~O!1) -> R(Y1~U) + S(PTP~O)  10^kcat_PTP
    R6B: R(Y1~P!1).S(PTP~O!1) -> R(Y1~U).S(PTP~O)  10^kcat_PTP

    # 1 Intra-complex binding: CSH2 bound, association of NSH2 (open) with other receptor
    R(Y2~P).S(NSH2~O,CSH2!+,PTP~O) <-> \
    R(Y2~P!1).S(NSH2~O!1,CSH2!+,PTP~O)  10^chi_r1*10^kon_NSH2, 10^koff_NSH2

    # 2 Intra-complex binding: CSH2 bound, association of PTP (open) with same receptor
    R(Y1~P,Y2~P!1).S(NSH2~O,CSH2!1,PTP~O) <-> \
    R(Y1~P!2,Y2~P!1).S(NSH2~O,CSH2!1,PTP~O!2)  10^chi_r2*10^kon_PTP, 10^koff_PTP

    # 3 Intra-complex binding: CSH2 bound, association of PTP (open) with other receptor
    R(Y1~P).R(Y2~P!1).S(NSH2~O,CSH2!1,PTP~O) <-> \
    R(Y1~P!2).R(Y2~P!1).S(NSH2~O,CSH2!1,PTP~O!2)  10^chi_r3*10^kon_PTP, 10^koff_PTP

    # 4 Intra-complex binding: NSH2 bound, association of CSH2 with other receptor
    R(Y2~P).S(NSH2~O!+,CSH2,PTP~O) <-> \
    R(Y2~P!1).S(NSH2~O!+,CSH2!1,PTP~O)  10^chi_r1*10^kon_CSH2, 10^koff_CSH2

    # 5 Intra-complex binding: NSH2 bound, association of PTP with other receptor
    R(Y1~P).R(Y2~P!1).S(NSH2~O!1,CSH2,PTP~O) <-> \
    R(Y1~P!2).R(Y2~P!1).S(NSH2~O!1,CSH2,PTP~O!2)  10^chi_r4*10^kon_PTP, 10^koff_PTP

    # 6 Intracomplex binding: NSH2 bound, association of PTP with same receptor
    R(Y1~P,Y2~P!1).S(NSH2~O!1,CSH2,PTP~O) <-> \
    R(Y1~P!2,Y2~P!1).S(NSH2~O!1,CSH2,PTP~O!2)  10^chi_r5*10^kon_PTP, 10^koff_PTP

    # 7 Intra-complex binding: PTP bound, association of CSH2 with same receptor
    R(Y1~P!1,Y2~P).S(NSH2~O,CSH2,PTP~O!1) <-> \
    R(Y1~P!1,Y2~P!2).S(NSH2~O,CSH2!2,PTP~O!1)  10^chi_r2*10^kon_CSH2, 10^koff_CSH2

    # 8 Intra-complex binding: PTP bound, association of CSH2 with other receptor
    R(Y1~P!1).R(Y2~P).S(NSH2~O,CSH2,PTP~O!1) <-> \
    R(Y1~P!1).R(Y2~P!2).S(NSH2~O,CSH2!2,PTP~O!1)  10^chi_r3*10^kon_CSH2, 10^koff_CSH2

    # 9 Intra-complex binding: PTP bound, association of NSH2 with other receptor
    R(Y1~P!1).R(Y2~P).S(NSH2~O,CSH2,PTP~O!1) <-> \
    R(Y1~P!1).R(Y2~P!2).S(NSH2~O!2,CSH2,PTP~O!1)  10^chi_r4*10^kon_NSH2, 10^koff_NSH2

    # 10 Intra-complex binding: PTP bound, association of NSH2 with same receptor
    R(Y1~P!1,Y2~P).S(NSH2~O,CSH2,PTP~O!1) <-> \
    R(Y1~P!1,Y2~P!2).S(NSH2~O!2,CSH2,PTP~O!1)  10^chi_r5*10^kon_NSH2, 10^koff_NSH2

    # 11 Intra-complex binding: CSH2 & NSH2 bound, assoc. of PTP with same receptor as CSH2
    R(Y1~P,Y2~P!1).R(Y2~P!2).S(NSH2~O!2,CSH2!1,PTP~O) <-> \
    R(Y1~P!3,Y2~P!1).R(Y2~P!2).S(NSH2~O!2,CSH2!1,PTP~O!3) \
    10^chi_r6*10^kon_PTP,10^koff_PTP

    # 12 Intra-complex binding: CSH2 & NSH2 bound, assoc. of PTP with same receptor as NSH2
    R(Y1~P,Y2~P!1).R(Y2~P!2).S(NSH2~O!1,CSH2!2,PTP~O) <-> \
    R(Y1~P!3,Y2~P!1).R(Y2~P!2).S(NSH2~O!1,CSH2!2,PTP~O!3) \
    10^chi_r7*10^kon_PTP, 10^koff_PTP

    # 13 Intra-complex binding: CSH2 & PTP bound to the same receptor, assoc. of NSH2
    R(Y1~P!1,Y2~P!2).R(Y2~P).S(NSH2~O,CSH2!2,PTP~O!1) <-> \
    R(Y1~P!1,Y2~P!2).R(Y2~P!3).S(NSH2~O!3,CSH2!2,PTP~O!1) \
    10^chi_r8*10^kon_NSH2, 10^koff_NSH2

    # 14 Intra-complex binding: CSH2 & PTP bound to different receptors, assoc. of NSH2
    R(Y2~P!1).R(Y1~P!2,Y2~P).S(NSH2~O,CSH2!1,PTP~O!2) <-> \
    R(Y2~P!1).R(Y1~P!2,Y2~P!3).S(NSH2~O!3,CSH2!1,PTP~O!2) \
    10^chi_r9*10^kon_NSH2, 10^koff_NSH2

    # 15 Intra-complex binding: PTP & NSH2 bound to different receptors, assoc. of CSH2
    R(Y2~P!1).R(Y1~P!2,Y2~P).S(NSH2~O!1,CSH2,PTP~O!2) <-> \
    R(Y2~P!1).R(Y1~P!2,Y2~P!3).S(NSH2~O!1,CSH2!3,PTP~O!2) \
    10^chi_r10*10^kon_CSH2, 10^koff_CSH2

    # 16 Intra-complex binding: PTP & NSH2 bound to same receptor, assoc. of CSH2 
    R(Y1~P!1,Y2~P!2).R(Y2~P).S(NSH2~O!2,CSH2,PTP~O!1) <-> \
    R(Y1~P!1,Y2~P!2).R(Y2~P!3).S(NSH2~O!2,CSH2!3,PTP~O!1) \
    10^chi_r11*10^kon_CSH2, 10^koff_CSH2
end reaction rules

begin observables
    Molecules  pYR  R(Y1~P!?)
end observables
end model

generate_network({overwrite=>1})

writeSBML()
simulate({method=>"ode",t_end=>700,n_steps=>7000,steady_state=>1,atol=>1e-10,rtol=>1e-8,sparse=>0})




            
