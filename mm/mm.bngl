# Michaelis-Menten
# Coded in BNGL by justin.s.hogg, 2013
begin model
begin parameters
    log_k1  -2.77  # log(/nM/s), logarithm is base 10
    log_k2  -1      # log(/s)
    log_k3  -2      # log(/s)
end parameters
begin molecule types
    S()
    E()
    ES()
    P()
end molecule types
begin seed species
    S()  600 # nM
    E()  6 # nM
end seed species
begin observables
    Molecules  S     S()
    Molecules  E     E()
    Molecules  ES    ES()
    Molecules  P     P()
end observables
begin functions
    kf()    10^(log_k1)
    kr()    10^(log_k2)
    kcat()  10^(log_k3)
end functions

begin reaction rules
    E + S <-> ES  kf(),kr()
    ES -> E + P   kcat()
end reaction rules
end model

# generate reaction network
generate_network({overwrite=>1})
simulate({method=>"ode",suffix=>"ode",t_end=>20000, n_steps=>2000, print_functions=>1})
writeSBML()